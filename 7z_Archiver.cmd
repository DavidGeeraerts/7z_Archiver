::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Author:		David Geeraerts																::
:: Location:	Olympia, Washington USA														::
:: E-Mail:		dgeeraerts.evergreen@gmail.com												::
::																							::
::	Copyright License																		::
:: 	Creative Commons: Attribution-NonCommercial-ShareAlike 3.0 Unported (CC BY-NC-SA 3.0)	::
:: 	http://creativecommons.org/licenses/by-nc-sa/3.0/										::
::																							::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

::::::::::::::::::::::::::::::::::::::::::::::::::
:: Helpful information can be found here:		::
::	http://thedeveloperblog.com/7-zip-examples	::
::::::::::::::::::::::::::::::::::::::::::::::::::

::::::::::::::::::::::::::::::::::
:: VERSIONING INFORMATION		::
::  Semantic Versioning used	::
::   http://semver.org/			::
::::::::::::::::::::::::::::::::::

@Echo Off
@SETLOCAL enableextensions

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
SET SCRIPT_NAME=Archiver
SET SCRIPT_VERSION=2.0.0
Title %SCRIPT_NAME% Version: %SCRIPT_VERSION%
Prompt 7z$G
color 8A
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Declare Global variables
::	All User variables are set within here.
::		(configure variables)
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:: ALL VARIABLES SHOULD BE CONFIGURED IN THE CONFIG FILE!
::	THESE ARE ONLY HERE TO ACT AS DEFAULTS

:: Logging
::	DEFAULT
:: ARCHIVE_LOG_LOCATION=%USERPROFILE%\Documents
:: ARCHIVE_LOG_FILE=%SCRIPT_NAME%_%SCRIPT_VERSION%_%COMPUTERNAME%.log

SET ARCHIVE_LOG_LOCATION=%USERPROFILE%\Documents
SET ARCHIVE_LOG_FILE=%SCRIPT_NAME%_%SCRIPT_VERSION%_%COMPUTERNAME%.log


::###########################################################################::
::            *******************
::            Advanced Settings 
::            *******************
::###########################################################################::


:: Config file fall back location
::	Location for Archiver Configuration File(s)
::		Path where the commandlet was executed from, appends "\" to the end
SET "ARCHIVER_CONFIG_DEFAULT_LOCATION=%~dp0"

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::
::##### Everything below here is 'hard-coded' [DO NOT MODIFY] #####
::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:: Calculate lapse time by capturing start time
::	Parsing %TIME% variable to get an interger number
FOR /F "tokens=1 delims=:." %%h IN ("%TIME%") DO SET S_hh=%%h
FOR /F "tokens=2 delims=:." %%h IN ("%TIME%") DO SET S_mm=%%h
FOR /F "tokens=3 delims=:." %%h IN ("%TIME%") DO SET S_ss=%%h
FOR /F "tokens=4 delims=:." %%h IN ("%TIME%") DO SET S_ms=%%h

::###########################################################################::
:: Configuration File Settings
::   and dependency check for configuration file!
::###########################################################################::
REM This has to be first to check that a configuration file actually exists
REM	will look for the 'Universal Configuration file, which will load the initial settings,
REM	that can than be overridden with a custom config file.
:://///////////////////////////////////////////////////////////////////////////
SET CONFIG_FILE_NAME=%SCRIPT_NAME%.config
SET ARCHIVER_CONFIG_SCHEMA_VERSION_MIN=1.0.0
IF DEFINED ARCHIVER_CONFIG_DEFAULT_LOCATION IF NOT EXIST "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%" GoTo errCONF ELSE (
	IF NOT EXIST %~dp0%CONFIG_FILE_NAME% GoTo errCONF)
:://///////////////////////////////////////////////////////////////////////////

::###########################################################################::
:: CONFIGURATION FILE OVERRIDE
::###########################################################################::

:: PARSER FORMAT
:: FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"<VARIABLE>" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "<VARIABLE>=%%V"
:: SET ERROR_CONFIG_PARSE_<VARIABLE>=%ERRORLEVEL%

:: ARCHIVER_CONFIG_SCHEMA_VERSION
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVER_CONFIG_SCHEMA_VERSION" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVER_CONFIG_SCHEMA_VERSION=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVER_CONFIG_SCHEMA_VERSION=%ERRORLEVEL%
:: CHECK the Config file Schema version meets the minimum requirement
SET ARCHIVER_CONFIG_FILE_SCHEMA_CHECK=0

::  Parse schema version from configuration file
FOR /F "tokens=1 delims=." %%V IN ("%ARCHIVER_CONFIG_SCHEMA_VERSION%") DO SET ARCHIVER_CONFIG_SCHEMA_VERSION_MAJOR=%%V
FOR /F "tokens=2 delims=." %%V IN ("%ARCHIVER_CONFIG_SCHEMA_VERSION%") DO SET ARCHIVER_CONFIG_SCHEMA_VERSION_MINOR=%%V
::  Parse schema version from minimum
FOR /F "tokens=1 delims=." %%V IN ("%ARCHIVER_CONFIG_SCHEMA_VERSION_MIN%") DO SET ARCHIVER_CONFIG_SCHEMA_VERSION_MIN_MAJOR=%%V
FOR /F "tokens=2 delims=." %%V IN ("%ARCHIVER_CONFIG_SCHEMA_VERSION_MIN%") DO SET ARCHIVER_CONFIG_SCHEMA_VERSION_MIN_MINOR=%%V

::  actual check
IF %ARCHIVER_CONFIG_SCHEMA_VERSION_MAJOR% GEQ %ARCHIVER_CONFIG_SCHEMA_VERSION_MIN_MAJOR% (SET /A "ARCHIVER_CONFIG_FILE_SCHEMA_CHECK=ARCHIVER_CONFIG_FILE_SCHEMA_CHECK+1") ELSE (GoTo err03)
IF %ARCHIVER_CONFIG_FILE_SCHEMA_CHECK% EQU 1 GoTo skipSC

IF %ARCHIVER_CONFIG_SCHEMA_VERSION_MINOR% GEQ %ARCHIVER_CONFIG_SCHEMA_VERSION_MIN_MINOR% (SET /A "ARCHIVER_CONFIG_FILE_SCHEMA_CHECK=ARCHIVER_CONFIG_FILE_SCHEMA_CHECK+1") ELSE (
     ECHO [WARN]	The configuration file [%CONFIG_FILE_NAME%] is using an older schema!)
IF %ARCHIVER_CONFIG_FILE_SCHEMA_CHECK% EQU 0 ECHO [WARN]	Minimum MINOR schema version not met. Will attempt to proceed anyway!
:skipSC


:: ARCHIVE_LOG_LOCATION
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_LOG_LOCATION" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_LOG_LOCATION=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_LOG_LOCATION=%ERRORLEVEL%
:: ARCHIVE_LOG_FILE
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_LOG_FILE" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_LOG_FILE=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_LOG_FILE=%ERRORLEVEL%
:: ARCHIVE_NAME
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_NAME" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_NAME=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_NAME=%ERRORLEVEL%
:: ARCHIVE_SOURCE_FOLDER
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_SOURCE_FOLDER" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_SOURCE_FOLDER=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_SOURCE_FOLDER=%ERRORLEVEL%
:: ARCHIVE_BULK_FOLDER
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_BULK_FOLDER" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_BULK_FOLDER=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_BULK_FOLDER=%ERRORLEVEL%
:: ARCHIVE_ACTION
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_ACTION" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_ACTION=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_ACTION=%ERRORLEVEL%
:: ARCHIVE_SWITCH_STRING
::	this is a work around due to multiple ='s in the switch string.
::	will enhance this feature in future version.
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_SWITCH_STRING" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET ARCHIVE_SWITCH_STRING=%%V
FOR /F "tokens=4 delims= " %%V IN ('FINDSTR /BC:"ARCHIVE_SWITCH_STRING" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_SWITCH_STRING=%ARCHIVE_SWITCH_STRING% %%V"
FOR /F "tokens=5 delims= " %%V IN ('FINDSTR /BC:"ARCHIVE_SWITCH_STRING" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_SWITCH_STRING=%ARCHIVE_SWITCH_STRING% %%V"
FOR /F "tokens=6 delims= " %%V IN ('FINDSTR /BC:"ARCHIVE_SWITCH_STRING" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_SWITCH_STRING=%ARCHIVE_SWITCH_STRING% %%V"
FOR /F "tokens=7 delims= " %%V IN ('FINDSTR /BC:"ARCHIVE_SWITCH_STRING" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_SWITCH_STRING=%ARCHIVE_SWITCH_STRING% %%V"
FOR /F "tokens=8 delims= " %%V IN ('FINDSTR /BC:"ARCHIVE_SWITCH_STRING" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_SWITCH_STRING=%ARCHIVE_SWITCH_STRING% %%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_SWITCH_STRING=%ERRORLEVEL%
:: ARCHIVE_CLEAN_UP
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_CLEAN_UP" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_CLEAN_UP=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_CLEAN_UP=%ERRORLEVEL%
:: ARCHIVE_FILE_EXCLUDE
FOR /F "tokens=2 delims=^=" %%V IN ('FINDSTR /BC:"ARCHIVE_FILE_EXCLUDE" "%ARCHIVER_CONFIG_DEFAULT_LOCATION%%CONFIG_FILE_NAME%"') DO SET "ARCHIVE_FILE_EXCLUDE=%%V"
SET ERROR_CONFIG_PARSE_ARCHIVE_FILE_EXCLUDE=%ERRORLEVEL%

::###########################################################################::

:start
:: Make sure log path exists, if not create, and use as a test
IF NOT EXIST %ARCHIVE_LOG_LOCATION% MD %ARCHIVE_LOG_LOCATION%
ECHO START %DATE% %TIME% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
systeminfo | FIND /I "Time Zone" >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
HOSTNAME >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
WHOAMI >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
systeminfo | FIND /I "OS NAME" >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ver >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%

::###########################################################################::
:: DEBUG VARIABLE OUTPUT
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO DEBUG OUTPUT...  >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%

ECHO [DEBUG]	ARCHIVER_CONFIG_DEFAULT_LOCATION: %ARCHIVER_CONFIG_DEFAULT_LOCATION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVER_CONFIG_DEFAULT_LOCATION ECHO [ERROR]	ARCHIVER_CONFIG_DEFAULT_LOCATION is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	CONFIG_FILE_PATH: %ARCHIVER_CONFIG_DEFAULT_LOCATION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED CONFIG_FILE_PATH ECHO [WARN]	CONFIG_FILE_PATH is blank! DEFAULT IN EFFECT! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	CONFIG_FILE_NAME: %CONFIG_FILE_NAME% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED CONFIG_FILE_NAME ECHO [ERROR]	CONFIG_FILE_NAME is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVER_CONFIG_SCHEMA_VERSION_MIN: %ARCHIVER_CONFIG_SCHEMA_VERSION_MIN% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVER_CONFIG_SCHEMA_VERSION_MIN ECHO [ERROR]	ARCHIVER_CONFIG_SCHEMA_VERSION_MIN is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVER_CONFIG_SCHEMA_VERSION: %ARCHIVER_CONFIG_SCHEMA_VERSION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVER_CONFIG_SCHEMA_VERSION ECHO [ERROR]	ARCHIVER_CONFIG_SCHEMA_VERSION is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_LOG_LOCATION: %ARCHIVE_LOG_LOCATION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_LOG_LOCATION ECHO [WARN]	ARCHIVE_LOG_LOCATION is blank! DEFAULT IN EFFECT! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_LOG_FILE: %ARCHIVE_LOG_FILE% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_LOG_FILE ECHO [WARN]	ARCHIVE_LOG_FILE is blank! DEFAULT IN EFFECT! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_NAME: %ARCHIVE_NAME% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_NAME ECHO [WARN]	ARCHIVE_NAME is blank! No single archiver to process. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_SOURCE_FOLDER: %ARCHIVE_SOURCE_FOLDER% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_SOURCE_FOLDER ECHO [WARN]	ARCHIVE_SOURCE_FOLDER is blank! No single archiver to process. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_BULK_FOLDER: %ARCHIVE_BULK_FOLDER% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_BULK_FOLDER ECHO [WARN]	ARCHIVE_BULK_FOLDER is blank! No bulk archive to process. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_ACTION: %ARCHIVE_ACTION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_ACTION ECHO [ERROR]	ARCHIVE_ACTION is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_SWITCH_STRING: %ARCHIVE_SWITCH_STRING% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_SWITCH_STRING ECHO [ERROR]	ARCHIVE_SWITCH_STRING is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_CLEAN_UP: %ARCHIVE_CLEAN_UP% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_CLEAN_UP ECHO [WARN]	ARCHIVE_CLEAN_UP is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ARCHIVE_FILE_EXCLUDE: %ARCHIVE_FILE_EXCLUDE% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF NOT DEFINED ARCHIVE_FILE_EXCLUDE ECHO [WARN]	ARCHIVE_FILE_EXCLUDE is blank! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO DEBUG CONFIGURATION PARSE ERRORS... >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVER_CONFIG_SCHEMA_VERSION: %ERROR_CONFIG_PARSE_ARCHIVER_CONFIG_SCHEMA_VERSION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_LOG_LOCATION: %ERROR_CONFIG_PARSE_ARCHIVE_LOG_LOCATION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_LOG_FILE: %ERROR_CONFIG_PARSE_ARCHIVE_LOG_FILE% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_DEBUG_TEST: %ERROR_CONFIG_PARSE_ARCHIVE_DEBUG_TEST% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_NAME: %ERROR_CONFIG_PARSE_ARCHIVE_NAME% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_SOURCE_FOLDER: %ERROR_CONFIG_PARSE_ARCHIVE_SOURCE_FOLDER% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_BULK_FOLDER: %ERROR_CONFIG_PARSE_ARCHIVE_BULK_FOLDER% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_ACTION: %ERROR_CONFIG_PARSE_ARCHIVE_ACTION% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_SWITCH_STRING: %ERROR_CONFIG_PARSE_ARCHIVE_SWITCH_STRING% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_CLEAN_UP: %ARCHIVE_CLEAN_UP% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO [DEBUG]	ERROR_CONFIG_PARSE_ARCHIVE_FILE_EXCLUDE: %ERROR_CONFIG_PARSE_ARCHIVE_FILE_EXCLUDE% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
::###########################################################################::


:: Make sure the command shell is pathed to the executable
PATH | FIND /I "7-Zip" || PATH=%PATH%;"C:\Program Files\7-Zip"
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%

::	##### SINGLE ARCHIVER ##### 
:SA
IF NOT DEFINED ARCHIVE_SOURCE_FOLDER GoTo skipSA
IF NOT DEFINED ARCHIVE_NAME GoTo skipSA

ECHO Processing Single Archive operation on [%ARCHIVE_SOURCE_FOLDER%]... >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
CD /D %ARCHIVE_SOURCE_FOLDER%

:: Variable names should not contain numeric character nor any special characters except for "_".
IF DEFINED ARCHIVE_SOURCE_FOLDER 7z %ARCHIVE_ACTION% %ARCHIVE_SWITCH_STRING% %ARCHIVE_FILE_EXCLUDE% "%ARCHIVE_NAME%" "%ARCHIVE_SOURCE_FOLDER%"

SET ARCHIVE_SA_ERRORLEVEL=%ERRORLEVEL%
ECHO ARCHIVE_SA_ERRORLEVEL: %ARCHIVE_SA_ERRORLEVEL% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_ERRORLEVEL% EQU 0 ECHO 7z reported no errors! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_ERRORLEVEL% EQU 1 ECHO 7z Warning Non fatal errors. For example, one or more files were locked by some other application, so they were not compressed. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_ERRORLEVEL% EQU 2 ECHO 7z Fatal error! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_ERRORLEVEL% EQU 7 ECHO 7z Command line error! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_ERRORLEVEL% EQU 8 ECHO 7z Not enough memory for operation! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_ERRORLEVEL% EQU 255 ECHO 7z reported that User stopped the process! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%

:: Create SHA256 hash
IF EXIST %ARCHIVE_SOURCE_FOLDER%\%ARCHIVE_NAME% 7z h -scrcsha256 "%ARCHIVE_SOURCE_FOLDER%\%ARCHIVE_NAME%.7z" >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
SET ARCHIVE_SA_SHA_ERRORLEVEL=%ERRORLEVEL%
ECHO ARCHIVE_SA_SHA_ERRORLEVEL: %ARCHIVE_SA_SHA_ERRORLEVEL% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_SHA_ERRORLEVEL% EQU 0 ECHO 7z reported no errors! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_SHA_ERRORLEVEL% EQU 1 ECHO 7z Warning Non fatal errors. For example, one or more files were locked by some other application, so they were not compressed. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_SHA_ERRORLEVEL% EQU 2 ECHO 7z Fatal error! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_SHA_ERRORLEVEL% EQU 7 ECHO 7z Command line error! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_SHA_ERRORLEVEL% EQU 8 ECHO 7z Not enough memory for operation! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
IF %ARCHIVE_SA_SHA_ERRORLEVEL% EQU 255 ECHO 7z reported that User stopped the process! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%

:: Not going to delete a folder that is set for bulk processing
:: Not going to delete the folder that the log file is logging to
IF DEFINED ARCHIVE_BULK_FOLDER GoTo skipSA
IF "%ARCHIVE_SOURCE_FOLDER%"=="%ARCHIVE_LOG_LOCATION%" GoTo skipSA
IF %ARCHIVE_CLEAN_UP% EQU 1 IF EXIST %ARCHIVE_SOURCE_FOLDER% RD /S /Q "%ARCHIVE_SOURCE_FOLDER%"

:skipSA


::	##### BULK ARCHIVE PROCESS ##### 
IF NOT DEFINED ARCHIVE_BULK_FOLDER GoTo skipBA
ECHO Processing Bulk Archive operation on [%ARCHIVE_BULK_FOLDER%]... >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%

IF DEFINED ARCHIVE_BULK_FOLDER GoTo BA ELSE (GoTo skipBA)
:BA
:: NOTES
::	When trying to catch the %ERRORLEVEL% from 7z and set the error level for 7z as ARCHIVE_BULK_CODE, it wouldn't set if 7z was configured output to file i.e. ">>"
::	...this is a workaround to the problem 
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
FOR /F %%Z IN ('DIR /B /A:D "%ARCHIVE_BULK_FOLDER%"') DO (7z %ARCHIVE_ACTION% %ARCHIVE_SWTICH_STRING% %ARCHIVE_FILE_EXCLUDE% "%ARCHIVE_BULK_FOLDER%\%%Z" "%ARCHIVE_BULK_FOLDER%\%%Z" >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%) & ((ECHO %ERRORLEVEL%)> %ARCHIVE_LOG_LOCATION%\var_ARCHIVER_BULK_CODE_%%Z.txt) & (IF EXIST "%ARCHIVE_BULK_FOLDER%\%%Z" 7z h -scrcsha256 "%ARCHIVE_BULK_FOLDER%\%%Z" >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%)

ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO Checking to see if source folders should be deleted up... >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO ARCHIVE_CLEAN_UP is set to: %ARCHIVE_CLEAN_UP% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
FOR /F %%K IN ('DIR /B /A:D "%ARCHIVE_BULK_FOLDER%"') Do (TYPE %ARCHIVE_LOG_LOCATION%\var_ARCHIVER_BULK_CODE_%%K.txt | FIND "0") && (IF %ARCHIVE_CLEAN_UP% EQU 1 IF EXIST "%ARCHIVE_BULK_FOLDER%\%%K" RD /S /Q "%ARCHIVE_BULK_FOLDER%\%%K") & (ECHO %%K ARCHIVER_BULK_CODE is 0 >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%)
:: Cleanup the var_ files
DEL /Q %ARCHIVE_LOG_LOCATION%\var_*.txt
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
:skipBA


:: Skip the error section
GoTo Time
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::                    START ERROR SECTION
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:://///////////////////////////////////////////////////////////////////////////
:: ERROR LEVEL 00's (DEPENDENCY ERROR)

:errCONF
:: ERROR CONF is a FATAL ERROR for NO configuration file
Color 4E
ECHO %TIME% [FATAL]	FATAL ERROR! NO CONFIGURATION FILE [.\%CONFIG_FILE_NAME%] FOUND! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
TIMEOUT 60
IF EXIST "%ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%" GoTo Time ELSE (EXIT)


:err03
:: ERROR 03 (Configuration file schema doesn't meet minimum version)
CLS
Color 4E
ECHO %TIME% [FATAL]	FATAL ERROR! CONFIGURATION FILE [.\%$CONFIG_FILE_NAME%] SCHEMA DOESN'T MEET MINIMUM VERSION! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO FATAL ERROR! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO CONFIGURATION FILE SCHEMA DOESN'T MEET MINIMUM VERSION! >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
TIMEOUT 60
IF EXIST "%ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%" GoTo Time ELSE (EXIT)







:://///////////////////////////////////////////////////////////////////////////





:Time
:: Calculate lapse time by capturing end time
::	Parsing %TIME% variable to get an interger number
FOR /F "tokens=1 delims=:." %%h IN ("%TIME%") DO SET E_hh=%%h
FOR /F "tokens=2 delims=:." %%h IN ("%TIME%") DO SET E_mm=%%h
FOR /F "tokens=3 delims=:." %%h IN ("%TIME%") DO SET E_ss=%%h
FOR /F "tokens=4 delims=:." %%h IN ("%TIME%") DO SET E_ms=%%h

:: Calculate the actual lapse time
IF %E_hh% GEQ %S_hh% (SET /A "L_hh=%E_hh%-%S_hh%") ELSE (SET /A "L_hh=%S_hh%-%E_hh%")
IF %E_mm% GEQ %S_mm% (SET /A "L_mm=%E_mm%-%S_mm%") ELSE (SET /A "L_mm=%S_mm%-%E_mm%")
IF %E_ss% GEQ %S_ss% (SET /A "L_ss=%E_ss%-%S_ss%") ELSE (SET /A "L_ss=%S_ss%-%E_ss%")
IF %E_ms% GEQ %S_ms% (SET /A "L_ms=%E_ms%-%S_ms%") ELSE (SET /A "L_ms=%S_ms%-%E_ms%")
:: turn hours into minutes and add to total minutes
IF %L_hh% GTR 0 SET /A "L_hhh=%L_hh%*60"
IF %L_hh% EQU 0 SET L_hhh=0
IF %L_hhh% GTR 0 SET /A "L_tm=%L_hhh%+%L_mm%"
IF %L_hhh% EQU 0 SET L_tm=%L_mm%
:: Lapse Time
ECHO Time Lapsed (mm:ss.ms): %L_tm%:%L_ss%.%L_ms% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%

:EOF
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO END %DATE% %TIME% >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
ECHO. >> %ARCHIVE_LOG_LOCATION%\%ARCHIVE_LOG_FILE%
Exit